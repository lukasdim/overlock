(* 
Valve has not yet released an API for deadlock, so I must use an unofficial one.
Their data is generated by people uploading the match files to obtain statistics
Games may not update for tens of minutes (or not show up at all).
I'm not sure how accurate it is, so it may be fast or it may be slow.
*)

open Domain.Types
open Domain.Json
open Transport.Mapper
open Deadlock.Types

open Yojson.Basic.Util

let get_req (url : string) : string =
  let c = Curl.init () in
  let errbuf = ref "" in
  Curl.set_errorbuffer c errbuf;

  Curl.set_url c url;
  Curl.set_followlocation c true;

  let exe_dir = Filename.dirname Sys.executable_name in 
  let ca_path = Filename.concat exe_dir "cacert.pem" in
  Curl.set_cainfo c ca_path;

  let buf = Buffer.create 4096 in
  Curl.set_writefunction c (fun s ->
    Buffer.add_string buf s;
    String.length s
  );

  let body =
    try
      Curl.perform c;
      Buffer.contents buf
    with
    | Curl.CurlException (code, msg, _) ->
        Printf.eprintf "CurlException: %s | msg=%s | errbuf=%s\n"
          (Curl.strerror code) (string_of_int msg) !errbuf;
        ""
  in

  Curl.cleanup c;
  body

let rec map3_shortest f xs ys zs =
  match (xs, ys, zs) with
  | (x :: xs', y :: ys', z :: zs') -> f x y z :: map3_shortest f xs' ys' zs'
  | _ -> []

let get_playerdata (steamid : Domain.Types.steamid3) : playerData =
  let json_str = get_req ("https://api.deadlock-api.com/v1/players/" ^ string_of_steamid64 steamid ^ "/match-history") in
  let json = convert_to_json json_str in

  (* Get lists of kills, deaths, and assists. Ex: very first match would span k[0] d[0] and a[0]*)
  let k : int list = get_json_ints "player_kills" json in
  let d : int list = get_json_ints "player_deaths" json in
  let a : int list = get_json_ints "player_assists" json in
  let hero_ids : int list = get_json_ints "hero_id" json in
  let heroes : hero list = List.map match_hero hero_ids in
  let age = last_opt (get_json_ints "start_time" json) in
  let age = Option.value age ~default:0 in

  let kdas : kda list = map3_shortest create_kda k d a in
  let mapped_kda : kda list HeroMap.t = build_kda_map heroes kdas in
  
  create_playerdata steamid age mapped_kda 

let rec take n xs =
  match n, xs with
  | 0, _ -> []
  | _, [] -> []
  | n, x :: xs -> x :: take (n - 1) xs

let sum_kda_list_first_n (n : int) (xs : kda list) : int * int * int =
  xs
  |> take n
  |> List.fold_left
       (fun (k_sum, d_sum, a_sum) (k : kda) ->
          ( k_sum + k.kills.kills
          , d_sum + k.deaths.deaths
          , a_sum + k.assists ))
       (0, 0, 0)

let get_most_recent_game (steamid : Domain.Types.steamid3) : Yojson.Basic.t option = 
  let json_str = get_req ("https://api.deadlock-api.com/v1/players/" ^ string_of_steamid64 steamid ^ "/match-history") in
  try
    let json = convert_to_json json_str in
    let list_json = to_list json in
    match list_json with
      | [] -> None
      | x :: list_json -> Some x
  with
  | Yojson.Json_error _ -> None
